buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net"
        }
        maven {
            name = "parchment"
            url = "https://maven.parchmentmc.org"
        }
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: "org.parchmentmc", name: "librarian", version: "1.+"
    }
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: "java"
apply plugin: "idea"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

version = "${minecraft_version}-" + getVersion()
group = "${group}"
archivesBaseName = "${mod_name}"

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "forge"
        url = "https://maven.minecraftforge.net"
    }
    maven {
        name = "ComputerCraft"
        url = "https://squiddev.cc/maven/"
    }
    maven {
        name = "Farmer's Delight"
        url = "https://www.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name "Patchouli"
        url "https://maven.blamejared.com"
        content {
            includeGroupByRegex "vazkii.*"
        }
    }
    maven {
        name = "JEI"
        url = "https://dvs1.progwml6.com/files/maven"
        content {
            includeGroup "mezz.jei"
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

sourceSets {
    main {
        resources {
            include '**/**'
            exclude '**/*.bbmodel'
            srcDirs += "$rootDir/src/generated/resources"
            exclude '.cache'
        }
    }
}

minecraft {
    mappings channel: 'parchment', version: '2022.03.13-1.18.2'

    runs {
        client {
            workingDirectory project.file("run")

            properties 'mixin.env.disableRefMap': 'true'

            mods {
                turtles {
                    source(sourceSets.main)
                }
            }
        }

        server {
            workingDirectory project.file("run")

            properties 'mixin.env.disableRefMap': 'true'

            mods {
                turtles {
                    source(sourceSets.main)
                }
            }
        }

        data {
            workingDirectory project.file("run")

            environment 'target', 'fmluserdevdata'

            properties 'fml.earlyprogresswindow': 'false'
            properties 'mixin.env.disableRefMap': 'true'

            args '--all', '--output', file('src/generated/resources/'),
                    '--reports',
                    '--mod', 'turtles', '--existing', sourceSets.main.resources.srcDirs[0]

            mods {
                turtles {
                    source(sourceSets.main)
                }
            }
        }
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    implementation fg.deobf("vazkii.patchouli:Patchouli:1.18.2-69")
    implementation fg.deobf("org.squiddev:cc-tweaked-1.18.2:1.100.4")

    implementation fg.deobf("curse.maven:farmersdelight-398521:3999153")
    implementation fg.deobf("curse.maven:pneumaticcraft-281849:3995275")

    compileOnly fg.deobf("mezz.jei:jei-1.18.2:9.7.0.192:api")
    runtimeOnly fg.deobf("mezz.jei:jei-1.18.2:9.7.0.192")
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : "${title}",
                "Specification-Vendor"    : "${team}",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "${team}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

artifacts {
    archives javadocJar
    archives srcJar
}

String getVersion() {
    String version = "${mod_version}"
    String branch = project.hasProperty("branch") ? project.branch : ""

    if (branch != "") {
        version += "-" + branch

        String buildNumber = getBuildNumber()
        if (buildNumber != "") {
            version += "." + buildNumber
        }
    }

    return version
}

static String getBuildNumber() {
    String buildNumber = System.getenv("BUILD_NUMBER") != null ? System.getenv("BUILD_NUMBER") : ""
    if (buildNumber == "") {
        buildNumber = System.getenv("bamboo_buildNumber") != null ? System.getenv("bamboo_buildNumber") : ""
    }
    return buildNumber
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "10000"
    }
}
